group = "waterdog.mobi"
version = "0.1"

buildscript {
    ext.versions = [
            "kotlin" : "1.2.71",
            "junit"  : "5.3.1",
            "koin"   : "1.0.1",
            "exposed": "0.11.2",
            "jackson": "2.9.7"
    ]

    ext.deps = [
            "kotlin"      : [
                    "stdlib"    : [
                            "jdk": "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}"
                    ],
                    "coroutines": [
                            "core": "org.jetbrains.kotlinx:kotlinx-coroutines-core:0.30.2"
                    ]
            ],
            "koin"        : [
                    "core"   : "org.koin:koin-core:${versions.koin}",
                    "coreExt": "org.koin:koin-core-ext:${versions.koin}",
                    "test"   : "org.koin:koin-test:${versions.koin}"
            ],
            "jackson"     : [
                    "core"    : "com.fasterxml.jackson.core:jackson-core:${versions.jackson}",
                    "databind": "com.fasterxml.jackson.core:jackson-databind:${versions.jackson}",
                    "kotlin"  : "com.fasterxml.jackson.module:jackson-module-kotlin:${versions.jackson}",
                    "jsr310"  : "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.2"
            ],
            "junit"       : [
                    "jupiterApi"   : "org.junit.jupiter:junit-jupiter-api:${versions.junit}",
                    "jupiterParams": "org.junit.jupiter:junit-jupiter-params:${versions.junit}",
                    "jupiterEngine": "org.junit.jupiter:junit-jupiter-engine:${versions.junit}"
            ],
            "exposed"     : "org.jetbrains.exposed:exposed:${versions.exposed}",
            "kluent"      : "org.amshove.kluent:kluent:1.42",
            "logback"     : "ch.qos.logback:logback-classic:1.2.3",
            "kafkaClients": "org.apache.kafka:kafka-clients:2.0.0",
            "rxJava2"     : "io.reactivex.rxjava2:rxjava:2.2.3",
            "h2"          : "com.h2database:h2:1.4.197",
            "hikari"      : "com.zaxxer:HikariCP:2.7.8",
    ]

    ext.isCiBuild = System.getenv("CI") == "true"

    repositories {
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "gradle.plugin.org.jlleitschuh.gradle:ktlint-gradle:5.0.0"
    }
}

apply plugin: "kotlin"
apply plugin: "org.jlleitschuh.gradle.ktlint"

compileKotlin.kotlinOptions.jvmTarget = "1.8"
compileTestKotlin.kotlinOptions.jvmTarget = "1.8"

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

test {
    useJUnitPlatform()
}

dependencies {
    implementation deps.kotlin.stdlib.jdk
    implementation deps.kotlin.coroutines.core
    implementation deps.logback
    implementation deps.exposed
    implementation deps.koin.core
    implementation deps.koin.coreExt
    implementation deps.kafkaClients
    implementation deps.rxJava2
    implementation deps.jackson.core
    implementation deps.jackson.databind
    implementation deps.jackson.kotlin
    implementation deps.jackson.jsr310

    testImplementation deps.junit.jupiterApi
    testImplementation deps.junit.jupiterParams
    testImplementation deps.kluent
    testImplementation deps.hikari
    testImplementation deps.h2
    testRuntimeOnly deps.junit.jupiterEngine
}